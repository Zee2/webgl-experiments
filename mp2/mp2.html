<!DOCTYPE HTML> 
<html lang="en"> 
<head>
<title>MP2</title>
<meta charset="utf-8">
<link rel="stylesheet" type="text/css" href="mp2.css">
<link href="https://fonts.googleapis.com/css?family=Roboto|Roboto+Mono&display=swap" rel="stylesheet">
</head>
 
<script id="shader-vs" type="x-shader/x-vertex">

  

  precision mediump float;

  attribute vec4 aVertexPosition;
  attribute vec3 aVertexNormal;
  
  uniform float time;

  varying vec3 vNormal;
  varying vec4 vWorldPos;
  varying vec3 vEyePos;

  uniform mat4 u_modelview;
  uniform mat4 u_projection;
  uniform mat3 u_normalMatrix;
  uniform float u_skyboxDraw;
  
  void main(void) {
      if(aVertexPosition.y <= 0.6){
        vNormal = vec3(0.0, 1.0, 0.0);
      } else {
        vNormal = aVertexNormal;
      }
      
      
      vec4 clamp_temp = aVertexPosition;
      clamp_temp.y = clamp(clamp_temp.y, 0.6 * (1.0 - u_skyboxDraw), 1000.);
      gl_Position = u_projection * u_modelview * clamp_temp;
      
      vWorldPos = clamp_temp;

      vec4 vEyePos4 = (u_modelview * vec4(aVertexPosition.xyz, 1.0));
      vEyePos = vEyePos4.xyz / vEyePos4.w;
  }
  </script>
    
  <script id="shader-fs" type="x-shader/x-fragment">
  precision mediump float;

  uniform mat4 u_modelview;
  uniform mat3 u_normalMatrix;

  uniform vec3 u_worldCameraPosition;

  uniform float time;
  uniform float u_shininess;
  uniform float u_useBlinnPhong;
  uniform float u_skyboxDraw;
  uniform float u_drawFog;
  uniform float u_drawClouds;

  varying vec4 vColor;
  varying vec4 vWorldPos;
  varying vec3 vEyePos;
  varying vec3 vNormal;

  #define SNOW vec4(0.9, 0.9, 0.9, 1)
  #define ROCK vec4(0.3, 0.3, 0.3, 1)
  #define GRASS vec4(0.1, 0.2, 0.05, 1)
  #define WATER vec4(0.1, 0.3, 0.4, 1)
  #define MAX 3.0

  // These could be uniforms, but I don't really feel
  // like exposing all of these to the user
  // (that's a lot of knobs and buttons)
  #define NUM_LIGHTS 2.
  #define AMBIENT_STRENGTH 0.15
  #define DIFFUSE_STRENGTH 0.9
  #define SPEC_STRENGTH 0.4

  // ---------------------------------------------------------------------
  //
  // LIBRARY CODE: Common 3D noise functions taken from ashima/webgl-noise
  //     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
  //               Distributed under the MIT License.
  //               https://github.com/ashima/webgl-noise
  //
  // ---------------------------------------------------------------------
  
  vec3 mod289(vec3 x) {
    return x - floor(x * (1.0 / 289.0)) * 289.0;
  }
  
  vec4 mod289(vec4 x) {
    return x - floor(x * (1.0 / 289.0)) * 289.0;
  }
  
  vec4 permute(vec4 x) {
        return mod289(((x*34.0)+1.0)*x);
  }
  
  vec4 taylorInvSqrt(vec4 r)
  {
    return 1.79284291400159 - 0.85373472095314 * r;
  }
  
  float snoise(vec3 v)
  { 
    const vec2  C = vec2(1.0/6.0, 1.0/3.0) ;
    const vec4  D = vec4(0.0, 0.5, 1.0, 2.0);
  
  // First corner
    vec3 i  = floor(v + dot(v, C.yyy) );
    vec3 x0 =   v - i + dot(i, C.xxx) ;
  
  // Other corners
    vec3 g = step(x0.yzx, x0.xyz);
    vec3 l = 1.0 - g;
    vec3 i1 = min( g.xyz, l.zxy );
    vec3 i2 = max( g.xyz, l.zxy );
  
    //   x0 = x0 - 0.0 + 0.0 * C.xxx;
    //   x1 = x0 - i1  + 1.0 * C.xxx;
    //   x2 = x0 - i2  + 2.0 * C.xxx;
    //   x3 = x0 - 1.0 + 3.0 * C.xxx;
    vec3 x1 = x0 - i1 + C.xxx;
    vec3 x2 = x0 - i2 + C.yyy; // 2.0*C.x = 1/3 = C.y
    vec3 x3 = x0 - D.yyy;      // -1.0+3.0*C.x = -0.5 = -D.y
  
  // Permutations
    i = mod289(i); 
    vec4 p = permute( permute( permute( 
                i.z + vec4(0.0, i1.z, i2.z, 1.0 ))
              + i.y + vec4(0.0, i1.y, i2.y, 1.0 )) 
              + i.x + vec4(0.0, i1.x, i2.x, 1.0 ));
  
  // Gradients: 7x7 points over a square, mapped onto an octahedron.
  // The ring size 17*17 = 289 is close to a multiple of 49 (49*6 = 294)
    float n_ = 0.142857142857; // 1.0/7.0
    vec3  ns = n_ * D.wyz - D.xzx;
  
    vec4 j = p - 49.0 * floor(p * ns.z * ns.z);  //  mod(p,7*7)
  
    vec4 x_ = floor(j * ns.z);
    vec4 y_ = floor(j - 7.0 * x_ );    // mod(j,N)
  
    vec4 x = x_ *ns.x + ns.yyyy;
    vec4 y = y_ *ns.x + ns.yyyy;
    vec4 h = 1.0 - abs(x) - abs(y);
  
    vec4 b0 = vec4( x.xy, y.xy );
    vec4 b1 = vec4( x.zw, y.zw );
  
    //vec4 s0 = vec4(lessThan(b0,0.0))*2.0 - 1.0;
    //vec4 s1 = vec4(lessThan(b1,0.0))*2.0 - 1.0;
    vec4 s0 = floor(b0)*2.0 + 1.0;
    vec4 s1 = floor(b1)*2.0 + 1.0;
    vec4 sh = -step(h, vec4(0.0));
  
    vec4 a0 = b0.xzyw + s0.xzyw*sh.xxyy ;
    vec4 a1 = b1.xzyw + s1.xzyw*sh.zzww ;
  
    vec3 p0 = vec3(a0.xy,h.x);
    vec3 p1 = vec3(a0.zw,h.y);
    vec3 p2 = vec3(a1.xy,h.z);
    vec3 p3 = vec3(a1.zw,h.w);
  
  //Normalise gradients
    vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
    p0 *= norm.x;
    p1 *= norm.y;
    p2 *= norm.z;
    p3 *= norm.w;
  
  // Mix final noise value
    vec4 m = max(0.6 - vec4(dot(x0,x0), dot(x1,x1), dot(x2,x2), dot(x3,x3)), 0.0);
    m = m * m;
    return 42.0 * dot( m*m, vec4( dot(p0,x0), dot(p1,x1), 
                                  dot(p2,x2), dot(p3,x3) ) );
  }

  // ---------------------------------------------------------------------
  //
  // END LIBRARY CODE SECTION
  //
  // ---------------------------------------------------------------------

  // Samples cloud density at point.
  float sampleDensity(vec3 pos){
    float clouds = clamp((snoise(pos * 0.8)) * 1.5, 0.0, 1.0) * clamp((snoise(pos * 4.0)) * 1.5, 0.0, 1.0);

    if(pos.y > 2.0){
      return mix(clouds, 0.0, smoothstep(2.0, 2.1, pos.y));
    } else if(pos.y > 1.9 && pos.y < 2.0){
      return clouds;
    } else {
      return mix(0.0, clouds, smoothstep(1.8, 1.9, pos.y));
    }
  }


  void main(void) {

    // We declare all the temp variables for the lighting formula here
    // so that we can seamlessly switch out the lighting algorithm
    // without dealing with a mess of variable declarations
    vec4 vLightPos4;
    vec3 vectorToLightSource;
    vec3 normalEye;
    float diffuseLightWeightning;
    vec3 reflectionVector;
    vec3 viewVectorEye;
    vec3 halfwayVector;
    float rdotv;
    float ndoth;
    float diffuse;
    float specWeighting;
    vec4 phongColor;
    vec3 vLightPos;

    vec4 baseColor;

    // First, calculate base color

    if(vWorldPos.y / MAX > 0.7){
      baseColor = mix(ROCK, SNOW, smoothstep(0.7, 0.705, vWorldPos.y / MAX));
    } else if(vWorldPos.y / MAX > 0.5){
      baseColor = mix(GRASS, ROCK, smoothstep(0.5, 0.7, vWorldPos.y / MAX));
    } else if(vWorldPos.y / MAX > 0.2){
      baseColor = mix(WATER, GRASS, smoothstep(0.2, 0.22, vWorldPos.y / MAX));
    } else {
      baseColor = WATER;
    }

    // Ambient light calculation
    // We do it first, as it's common to both Phong and Blinn-Phong,
    // and should not be done per-light (Only one ambient source!)
    gl_FragColor = AMBIENT_STRENGTH * baseColor;

    // Transform the normal (n) to eye coordinates
    normalEye = normalize(u_normalMatrix * vNormal);
    
    // The camera in eye coordinates is located in the origin and is pointing
    // along the negative z-axis. Calculate viewVector (v) 
    // in eye coordinates as:
    // (0.0, 0.0, 0.0) - vertexPositionEye3
    viewVectorEye = -normalize(vEyePos);

    for(float i = 0.; i < NUM_LIGHTS; i++){

      vLightPos4 = (u_modelview * vec4(5.0*cos(time * 0.001 + ((2.*3.141)/NUM_LIGHTS) * i) + 7., 3.0, 5.0*sin(time * 0.001 + ((2.*3.141)/NUM_LIGHTS) * i) + 7., 1.0));
      //vec4 vLightPos4 = (u_modelview * vec4(4.0, 2.0, 4.0, 1.0));
      vLightPos = vLightPos4.xyz / vLightPos4.w;



      // Calculate the vector (l) to the light source
      vectorToLightSource = normalize(vLightPos - vEyePos);
      
      // Calculate n dot l for diffuse lighting
      diffuseLightWeightning = max(dot(normalEye, 
                                      vectorToLightSource), 0.0);
                                        
      // Calculate the reflection vector (r) that is needed for specular light
      reflectionVector = normalize(reflect(-vectorToLightSource, 
                                              normalEye));

      // Calculate the halfway vector (h)
      halfwayVector = (vectorToLightSource + viewVectorEye) / length(vectorToLightSource + viewVectorEye);

      // RdotV, for phong model only
      rdotv = max(dot(reflectionVector, viewVectorEye), 0.0);

      // NdotH, for blinn-phong
      ndoth = max(dot(normalEye, halfwayVector), 0.0);

      diffuse = max(dot(normalEye, vectorToLightSource), 0.0);

      // Depending on the currently selected shading model (Phong or Blinn-Phong)
      // we change the dot product we perform. (RdotV or NdotH)
      if(u_useBlinnPhong > 0.5){
        specWeighting = pow(rdotv, u_shininess);
      } else {
        specWeighting = pow(ndoth, u_shininess);
      }
      
      
      // Perform the rest of the shading calculation.
      // We don't do the ambient here, we already did it above (before the shading calculation)
      gl_FragColor += vec4(((vec3(DIFFUSE_STRENGTH)*baseColor.xyz) * diffuse
                        + (vec3(SPEC_STRENGTH)*vec3(1.0)) * specWeighting),1.0);

      
      
    }

    // Get adjusted world-space camera position
    vec3 modifiedCameraPosition = vec3(u_worldCameraPosition.x * -1., u_worldCameraPosition.y, u_worldCameraPosition.z * -1.);
    vec3 ray = vWorldPos.xyz - modifiedCameraPosition;

    if(u_skyboxDraw < 0.5){
      // Perform logarithmic distance fog calculation
      float distance_fog = clamp(log(clamp(length(ray) * 0.4 - 0.5, 0., 10.)), 0., 0.9);
      gl_FragColor = mix(gl_FragColor, vec4(1.), distance_fog * u_drawFog);
    } else {
      gl_FragColor = vec4(0., 0., 0., 0.0);
    }
    


    if(u_drawClouds > 0.5){
      // Raymarch through cloud layer to render fully volumetric clouds!
      float accum_density;
      float raymarch = 0.0;
      for(int iter = 0; iter < 100; iter++){
        accum_density += sampleDensity(modifiedCameraPosition + normalize(ray) * raymarch) * 0.5;
        raymarch += 0.04;
        
        if(raymarch > length(ray) + 0.0) break;
      }

      //vec4(vec3(avg_altitude - 2.0), 1.0)
      gl_FragColor = mix(gl_FragColor * (1.0 - u_skyboxDraw), vec4(1.0), clamp(pow(accum_density * 0.4, 1.), 0., 1.0));
    }

    if(u_skyboxDraw > 0.5){
      gl_FragColor = mix(gl_FragColor, vec4(1.), 1. - smoothstep(0.0,3.0, vWorldPos.y));
    }
    
    
  }
  </script>

<body style = "font-family: roboto; color: rgb(0, 0, 0); background-color: #aed1ff" onload="startup();">
  <div class = "header" >
    <h1 style = "font-family: roboto mono; ">
      CS418 MP2
    </h1>
    <p style = "font-family: roboto mono">
      Finn Sinclair - 2019
    </p>
    
    <div class="uiContainer">
      <div class="controls">
          <img src="controls.png" width="500">
      </div>
      <div id="ui">
        <div class="slidecontainer">
          <input type="range" min="10" max="500" value="100" class="slider" id="shininess"> Shininess
        </div>
        <div class="radiobutton">
          <input type="radio" class="radiobutton" value="yes" name="use_blinnphong" checked> Use Blinn-Phong
          <input type="radio" class="radiobutton" value="no" name="use_blinnphong"> Use Phong
        </div>
        <input type="checkbox" class="radiobutton" name="use_fog" checked> Render distance fog <br>
        <input type="checkbox" class="radiobutton" name="use_clouds" unchecked> Render volumetric raymarched clouds
        <div class="sphereButton">
          <input type="button" name="toggle_sphere" id="toggle_sphere"value="Toggle sphere material visualization"> 
        </div>
        
      </div>
      
    </div>
  </div>
  <canvas class="canvas" id="myGLCanvas" width="1000" height = "600"></canvas>
  
  
</body>

<script src="scripts/gl-matrix.js"></script>   
<script src="scripts/webgl-utils.js"></script>
<script src="scripts/mouseControls.js"></script>
<script src="scripts/keyboardControl.js"></script>
<script src="scripts/simpleModeling.js"></script>
<script src="scripts/helpers.js"></script>
<script src="scripts/terrain_demo.js"></script>  
<script src="scripts/mp2.js"></script>   

</html>
